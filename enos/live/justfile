RUN := "poetry run python integration.py"

# Entrypoint, run deploy and then tunnels
_default:
    @just --choose

# Deploy the stack to redy the eperiment on grid'5000
deploy $CLUSTER="gros" +UP_FLAGS='':
    {{RUN}} up {{UP_FLAGS}}
    {{RUN}} k3s-deploy
    {{RUN}} monitoring

# Refresh the container images hosted by k3s on all deployed nodes
refresh:
    {{RUN}} k3s-deploy

# Open SSH tunnels
tunnels +FLAGS='':
    {{RUN}} tunnels {{FLAGS}}

# List end nodes of the Fog network
endpoints:
	{{RUN}} endpoints

# expe to target node_id for n repetitions with a delay between each
expe market_port node_id n='50' delay='30':
	./expe.sh {{n}} {{node_id}} {{delay}} {{market_port}}


# Delete the Job on grid'5000 and local EnosLib files
clean:
    {{RUN}} clean || true
    rm -rf enos_* current cachedir __enos*

collect $port $period="1h":
    poetry run python collect.py

lab:
	jupyter lab
